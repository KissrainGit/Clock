RS		BIT		P2.6
RW		BIT		P2.5
E		BIT		P2.7
FENG	BIT		P1.4
SEC		EQU		30H
MIN		EQU		31H
HOUR	EQU		32H
YE		EQU		33H
AR		EQU		34H
JS		EQU		35H
TXS		EQU		36H
SEC1	EQU		37H
MIN1	EQU		38H
HOUR1	EQU		39H
JH1		EQU		40H
JM1		EQU		41H
JS1		EQU		42H
JH2		EQU		43H
JM2		EQU		44H
JS2		EQU		45H
JH3		EQU		46H
JM3		EQU		47H
JS3		EQU		48H
SHI1	EQU		51H
SHI2	EQU		52H
FEN1	EQU		54H
FEN2	EQU		55H
MIAO1	EQU		57H
MIAO2	EQU		58H
FCT		EQU		50H;P0口用作输出LCD，P1口用作读取键盘且采用中断方式，P2辅助控制LCD。
//宏定义
		ORG		0000H
		LJMP	START
		ORG		000BH
		LJMP	ZDT0
		ORG		0030H
START:	MOV		SP,#70H
		MOV		R0,#SHI1
		MOV		R1,#JH1
		MOV		FCT,#-1;-1为正常工作，0为调整时间界面，1为调整闹钟界面，-2为响铃界面,-3为记录时间界面
		MOV		HOUR1,#6
		SETB	IT0
		MOV		TMOD,#01H
		MOV		TL0,#0DCH
		MOV		TH0,#0BH
		MOV		R6,#16
		LCALL	KAIJI
		SETB	EA
		SETB	EX0
		MOV		P1,#0FH
		SETB	TR0
		SETB	ET0;中断初始化（外部键盘中断，内部T0中断）
		LJMP	MAIN
//初始化程序并放开机动画


MAIN:	LCALL	KEY
		LCALL	XS0
		LCALL	NAO
		LCALL	XIANG
		LJMP	MAIN	
//主程序用于轮询子程序


KAIJI:	MOV		P0,#01H;清屏
		LCALL	LOAD
		MOV		P0,#38H;选择功能模式
		LCALL	LOAD
		MOV		P0,#0CH;显示亮屏以及光标选项
		LCALL	LOAD
		MOV		P0,#06H;更改光标设置
		LCALL	LOAD
		MOV		DPTR,#TAB
KAIJI1:	CLR		A
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		INC		DPTR
		CJNE	A,#0EFH,KAIJI1
		MOV		R7,#8
KAIJI2:	MOV		P0,#0A1H
		LCALL	DISPLAY
		DJNZ	R7,KAIJI2
		MOV		R7,#8
		MOV		P0,#88H
		LCALL	LOAD
KAIJI3:	MOV		P0,#1FH
		LCALL	LOAD
		CPL		FENG
		LCALL	DELAY
		DJNZ	R7,KAIJI3		
		RET;开机动画子程序
//开机动画


JIN:	MOV		A,SEC
		CJNE	A,#60,JIN1
		INC		MIN
		MOV		SEC,#0		
JIN1:	MOV		A,MIN
		CJNE	A,#60,JIN2
		INC		HOUR
		MOV		MIN,#0
JIN2:	MOV		A,HOUR
		CJNE	A,#24,JIN3
		MOV		HOUR,#0
JIN3:	RET
//进制计算


XS0:	MOV		A,TXS
		CJNE	A,#0,XS00
		RET
XS00:	MOV		TXS,#0
		MOV		DPTR,#XS
		MOV		A,FCT;按照fct的值选择显示内容
		INC		A
		INC		A
		INC		A
		RL		A
		JMP		@A+DPTR
XS:		AJMP	XS1
		AJMP	XS2
		AJMP	XS3
		AJMP	XS4
		AJMP	XS5
XS1:	LCALL	RECORD		
		RET
XS2:	LCALL	XIANGQI
		RET;闹钟响起界面	
XS3:	LCALL	SHIJIAN
		RET;计时界面
XS4:	LCALL	TIME
		RET;调整时间界面
XS5:	LCALL	CLOCK
		RET;调整闹钟的界面
//显示界面分流


XIANGQI:MOV		P0,#01H
		LCALL	LOAD
		MOV		P0,#0CH
		LCALL	LOAD
		MOV		DPTR,#TAB3
XIANGQI1:	CLR		A
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		INC		DPTR
		CJNE	A,#88H,XIANGQI1
		RET
//定时响铃界面


RECORD:	PUSH	01H
		MOV		P0,#01H;计数器界面
		LCALL	LOAD
		MOV		P0,#0CH
		LCALL	LOAD
		MOV		A,JS
		CJNE	A,#0,RECORD1
		MOV		R1,#JH1
		LJMP	RECORD2
RECORD1:MOV		R1,#JH2
RECORD2:MOV		P0,#80H
		LCALL	LOAD
		MOV		DPTR,#TAB1
		MOV		A,#1
		ADD		A,JS
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		P0,#2EH
		LCALL	DISPLAY
		MOV		A,@R1
		MOV		B,#10
		DIV		AB
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		P0,#3AH
		LCALL	DISPLAY;显示1时
		INC		R1
		MOV		A,@R1
		MOV		B,#10
		DIV		AB
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		P0,#3AH
		LCALL	DISPLAY;显示1分钟
		INC		R1
		MOV		A,@R1
		MOV		B,#10
		DIV		AB
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY;显示1秒
		INC		R1
		MOV		P0,#0C0H
		LCALL	LOAD
		MOV		DPTR,#TAB1
		MOV		A,#2
		ADD		A,JS
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		P0,#2EH
		LCALL	DISPLAY
		MOV		A,@R1
		MOV		B,#10
		DIV		AB
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		P0,#3AH
		LCALL	DISPLAY;显示2时
		INC		R1
		MOV		A,@R1
		MOV		B,#10
		DIV		AB
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		P0,#3AH
		LCALL	DISPLAY;显示2分钟
		INC		R1
		MOV		A,@R1
		MOV		B,#10
		DIV		AB
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY;显示2秒
		POP		01H
		RET
//记录时间界面


SHIJIAN:INC		YE
		MOV		P0,#01H
		LCALL	LOAD
		MOV		P0,#84H
		LCALL	LOAD
		MOV		A,HOUR
		MOV		B,#10
		DIV		AB
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		P0,#3AH
		LCALL	DISPLAY;显示小时
		MOV		A,MIN
		MOV		B,#10
		DIV		AB
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		P0,#3AH
		LCALL	DISPLAY;显示分钟
		MOV		A,SEC
		MOV		B,#10
		DIV		AB
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY;显示秒
		MOV		P0,#0C0H
		LCALL	LOAD
		MOV		A,AR
		CJNE	A,#0,SHIJIAN3
		MOV		A,YE
		MOV		B,#2
		DIV		AB
		MOV		A,B
		JZ		SHIJIAN2
		MOV		DPTR,#TAB6
		LJMP	SHIJIAN1
SHIJIAN2:MOV		DPTR,#TAB5
		LJMP	SHIJIAN1
SHIJIAN3:MOV		AR,#0
		MOV		DPTR,#TAB7
SHIJIAN1:CLR		A
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		INC		DPTR
		CJNE	A,#88H,SHIJIAN1	
		RET
TIME:	MOV		P0,#01H
		LCALL	LOAD
		MOV		P0,#84H
		LCALL	LOAD
		MOV		A,HOUR
		MOV		B,#10
		DIV		AB
		MOV		SHI1,A
		MOV		SHI2,B
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		P0,#3AH
		LCALL	DISPLAY;显示小时
		MOV		A,MIN
		MOV		B,#10
		DIV		AB
		MOV		FEN1,A
		MOV		FEN2,B
		MOV		DPTR,#TAB1
		MOVC	A,@	A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		P0,#3AH
		LCALL	DISPLAY;显示分钟
		MOV		A,SEC
		MOV		B,#10
		DIV		AB
		MOV		MIAO1,A
		MOV		MIAO2,B
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY;显示秒
		MOV		P0,#0C4H
		LCALL	LOAD
		MOV		DPTR,#TAB4
TIME1:	CLR		A
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		INC		DPTR
		CJNE	A,#88H,TIME1
		MOV		P0,#0EH;打开光标并闪烁
		LCALL	LOAD
		MOV		A,R0
		ADD		A,#33H
		MOV		P0,A
		LCALL	LOAD;光标停留再对应位置
		RET
CLOCK:	MOV		P0,#01H
		LCALL	LOAD
		MOV		P0,#84H
		LCALL	LOAD
		MOV		A,HOUR1
		MOV		B,#10
		DIV		AB
		MOV		SHI1,A
		MOV		SHI2,B
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		P0,#3AH
		LCALL	DISPLAY;显示小时
		MOV		A,MIN1
		MOV		B,#10
		DIV		AB
		MOV		FEN1,A
		MOV		FEN2,B
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		P0,#3AH
		LCALL	DISPLAY;显示分钟
		MOV		A,SEC1
		MOV		B,#10
		DIV		AB
		MOV		MIAO1,A
		MOV		MIAO2,B
		MOV		DPTR,#TAB1
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		MOV		A,B
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY;显示秒
		MOV		P0,#0C6H
		LCALL	LOAD
		MOV		DPTR,#TAB2
CLOCK1:	CLR		A
		MOVC	A,@A+DPTR
		MOV		P0,A
		LCALL	DISPLAY
		INC		DPTR
		CJNE	A,#88H,CLOCK1
		MOV		P0,#0EH;打开光标并闪烁
		LCALL	LOAD
		MOV		A,R0
		ADD		A,#33H
		MOV		P0,A
		LCALL	LOAD;光标停留再对应位置		
		RET
//具体的界面排布


NAO:	MOV		A,FCT
		CJNE	A,#-1,NAO1
		MOV		A,SEC1
		CJNE	A,SEC,NAO1
		MOV		A,MIN1
		CJNE	A,MIN,NAO1
		MOV		A,HOUR1
		CJNE	A,HOUR,NAO1
		MOV		FCT,#-2;判断闹钟是否应该响起，若是则使fct为-2
NAO1:	RET
//判断是否该响铃


XIANG:	MOV		A,FCT
		CJNE	A,#-2,XIANG1
		LCALL	XS0
		CPL		FENG
		LCALL	DEL
		JB		P1.0,XIANG
		MOV		FCT,#-1
		LJMP	XIANG
XIANG1:	RET
//发出响铃


ZDT0:	PUSH	ACC
		MOV		TL0,#0DCH
		MOV		TH0,#0BH
		CJNE	R6,#8,ZDT01
		DEC		R6
		LJMP	ZDT03
ZDT01:	DJNZ	R6,RET0
		MOV		R6,#16
		INC		SEC
		MOV		R7,FCT
		CJNE	R7,#0,ZDT02
		DEC		SEC
ZDT02:	LCALL	JIN
ZDT03:	MOV		TXS,#1
RET0:	POP		ACC
		RETI;定时器中断0
//计时器中断T0


KEY:	MOV		A,P1
		CJNE	A,#0FH,KEY1
		RET
KEY1:	LCALL	ZDX0
		RET
//按键检测


ZDX0:	LCALL	DEL
		MOV		A,P1
		CJNE	A,#0FH,ZDX01
		LJMP	RET1;去抖
ZDX01:	MOV		R2,#0F7H
		MOV		R3,#7FH
		MOV		64H,#-1
		MOV		65H,#-1
LIE:	MOV		A,R2
		RL		A
		MOV		R2,A
		ANL		A,#0F0H
		ORL		A,#0FH
		MOV		P1,A
		MOV		A,P1
		ANL		A,#0FH
		INC		64H
		CJNE	A,#0FH,HANG
		LJMP	LIE;读列数
HANG:	MOV		A,P1
		ANL		A,#0FH
		MOV		B,A
		MOV		A,R3
		RL		A
		MOV		R3,A
		ANL		A,#0FH
		INC		65H
		CJNE	A,B,HANG;读行数
JISUAN:	MOV		A,65H
		RL		A
		RL		A
		ADD		A,64H;得出键盘号
		MOV		B,#3
		MUL		AB
		PUSH	ACC
SONGJIAN:MOV	P1,#0FH
		MOV		A,P1
		CJNE	A,#0FH,SONGJIAN//松键检测
		POP		ACC
		MOV		DPTR,#GN
		JMP		@A+DPTR
GN:		LJMP	JW
		LJMP	J14
		LJMP	J13
		LJMP	J12
		LJMP	J11
		LJMP	J9
		LJMP	J6
		LJMP	J3
		LJMP	J0
		LJMP	J8
		LJMP	J5
		LJMP	J2
		LJMP	J10
		LJMP	J7
		LJMP	J4
		LJMP	J1
J12:	MOV		FCT,#0
		LJMP	RET1;修改时间控制键
J13:	MOV		FCT,#1
		LJMP	RET1;修改闹钟控制键
J14:	MOV		A,FCT
		CJNE	A,#-1,J140
		CJNE	R1,#49H,J141
		MOV		R1,#JH3
		MOV		JH1,JH2
		MOV		JH2,JH3
		MOV		JM1,JM2
		MOV		JM2,JM3
		MOV		JS1,JS2
		MOV		JS2,JS3
		LJMP	J141
J140:	MOV		FCT,#-1
		LJMP	RET1
J141:	MOV		@R1,HOUR
		INC		R1
		MOV		@R1,MIN
		INC		R1
		MOV		@R1,SEC
		INC		R1
		MOV		AR,#1		
		LJMP	RET1;时间界面进行计时
J10:	MOV		A,FCT
		CJNE	A,#-3H,J101
		MOV		JS,#0
		LJMP	RET1
J101:	CJNE	R0,#SHI1,J100
		LJMP	RET1
J100:	DEC		R0
		MOV		P0,#04H
		LCALL	LOAD
		LJMP	RET1;左键
J11:	MOV		A,FCT	
		CJNE	A,#-3H,J111
		MOV		JS,#1
		LJMP	RET1
J111:	CJNE	R0,#MIAO2,J110
		LJMP	RET1
J110:	INC		R0
		MOV		P0,#06H
		LCALL	LOAD
		LJMP	RET1;右键
J0:		MOV		A,FCT
		CJNE	A,#-1,J00
		MOV		HOUR,#0
		MOV		MIN,#0
		MOV		SEC,#0
		MOV		JH1,#0
		MOV		JH2,#0
		MOV		JH3,#0
		MOV		JM1,#0
		MOV		JM2,#0
		MOV		JM3,#0
		MOV		JS1,#0
		MOV		JS2,#0
		MOV		JS3,#0
		MOV		R1,#JH1
		LJMP	RET1;时间界面清零时间已经计时器
J00:	MOV		@R0,#0
		LCALL	JSHU
		LJMP	RET1
J1:		MOV		@R0,#1
		LCALL	JSHU
		LJMP	RET1
J2:		MOV		@R0,#2
		LCALL	JSHU
		LJMP	RET1
J3:		MOV		@R0,#3
		LCALL	JSHU
		LJMP	RET1
J4:		MOV		@R0,#4
		LCALL	JSHU
		LJMP	RET1
J5:		MOV		@R0,#5
		LCALL	JSHU
		LJMP	RET1
J6:		MOV		@R0,#6
		LCALL	JSHU
		LJMP	RET1
J7:		MOV		@R0,#7
		LCALL	JSHU
		LJMP	RET1
J8:		MOV		@R0,#8
		LCALL	JSHU
		LJMP	RET1
J9:		MOV		@R0,#9
		LCALL	JSHU
		LJMP	RET1
JW:		MOV		A,FCT
		CJNE	A,#1,JW2
		MOV		SEC1,SEC
		MOV		MIN1,MIN
		MOV		HOUR1,HOUR
		LJMP	RET1
JW2:	MOV		FCT,#-3
		LJMP	RET1
JSHU:	MOV		A,FCT
		JB		ACC.7,JSHU1
		CJNE	A,#0,JSHU2
		LCALL	TIME_DO
		LJMP	JSHU1
JSHU2:	LCALL	CLOCK_DO
JSHU1:	RET
TIME_DO:MOV		A,SHI1
		MOV		B,#10
		MUL		AB
		ADD		A,SHI2
		CLR		C
		PUSH	ACC
		SUBB	A,#24
		JNB		ACC.7,RET2
		POP		ACC
		MOV		HOUR,A
		MOV		A,FEN1
		MOV		B,#10
		MUL		AB
		ADD		A,FEN2
		CLR		C
		PUSH	ACC
		SUBB	A,#60
		JNB		ACC.7,RET2
		POP		ACC
		MOV		MIN,A
		MOV		A,MIAO1
		MOV		B,#10
		MUL		AB
		ADD		A,MIAO2
		CLR		C
		PUSH	ACC
		SUBB	A,#60
		JNB		ACC.7,RET2
		POP		ACC
		MOV		SEC,A
		RET
CLOCK_DO:MOV	A,SHI1
		MOV		B,#10
		MUL		AB
		ADD		A,SHI2
		CLR		C
		PUSH	ACC
		SUBB	A,#24
		JNB		ACC.7,RET2
		POP		ACC
		MOV		HOUR1,A
		MOV		A,FEN1
		MOV		B,#10
		MUL		AB
		ADD		A,FEN2
		CLR		C
		PUSH	ACC
		SUBB	A,#60
		JNB		ACC.7,RET2
		POP		ACC
		MOV		MIN1,A
		MOV		A,MIAO1
		MOV		B,#10
		MUL		AB
		ADD		A,MIAO2
		CLR		C
		PUSH	ACC
		SUBB	A,#60
		JNB		ACC.7,RET2
		POP		ACC
		MOV		SEC1,A
		RET
RET2:	POP		ACC;弹出因为跳转而产生的垃圾
		RET
RET1:	MOV		P1,#0FH
		RET
//键盘输入并执行

							
LOAD:	CLR		RS
		CLR		RW
		CLR		E
		LCALL	DEL
		SETB	E
		RET;lcd装载命令
//


DISPLAY:SETB	RS
		CLR		RW
		CLR		E
		LCALL	DEL
		SETB	E
		RET;lcd显示
//



DELAY:	MOV		R2,#8
DELAY1:	MOV		R3,#200
DELAY2:	MOV		R4,#123
DELAY3:	DJNZ	R4,DELAY3
		DJNZ	R3,DELAY2
		DJNZ	R2,DELAY1
		RET;长延时子程序
//


DEL:	MOV		R2,#100
DEL1:	MOV		R3,#8
		DJNZ	R3,$
		DJNZ	R2,DEL1
		RET;短延时
//


TAB:	DB	'Welcome ',0EFH
TAB1:	DB	'0123456789'
TAB2:	DB	'Clock',88H
TAB3:	DB	'Notice Time up!!',88H
TAB4:	DB	'Set Time',88H
TAB5:	DB	01001111B,01011111B,01101111B,01111111B,'      ','Yiming ',88H
TAB6:	DB	01001111B,01011110B,01001111B,' ',01111111B,'     ','Yiming',88H
TAB7:	DB	'Record    Yiming',88H
END
